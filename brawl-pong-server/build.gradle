plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.20'
    id 'com.github.node-gradle.node' version "4.0.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'application'
}

group = 'io.sourceempire.brawlpong'
version = '1.0'

repositories {
    mavenCentral()
}

application {
    mainClassName = 'io.vertx.core.Launcher'
}

jar {
    manifest {
        attributes 'Main-Class': 'io.sourceempire.brawlpong.MainKt'
    }
}

shadowJar {
    archiveClassifier.set('fat')
    manifest {
        attributes 'Main-Verticle': 'io.sourceempire.brawlpong.Main'
    }
    mergeServiceFiles()
}

dependencies {
    // Vert.x
    implementation 'io.vertx:vertx-core:4.4.1'
    implementation "io.vertx:vertx-lang-kotlin:4.4.1"
    implementation 'io.vertx:vertx-web:4.4.1'
    implementation 'io.vertx:vertx-web-client:4.4.1'
    implementation 'io.vertx:vertx-sockjs-service-proxy:4.4.1'
    implementation 'io.vertx:vertx-auth-jwt:4.4.1'
    implementation 'io.vertx:vertx-zookeeper:4.4.1'

    // Jackson
    implementation "com.fasterxml.jackson.core:jackson-databind:2.14.0"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.13.1"

    // Testing
    testImplementation "io.vertx:vertx-junit5:4.4.1"
}

tasks.register('installPongDependencies', NpmTask) {
    args = ['install']
    workingDir = file('../brawl-pong-client')
}

tasks.register('buildPong', NpmTask) {
    dependsOn installPongDependencies
    args = ['run', 'build']
    workingDir = file('../brawl-pong-client')
}

tasks.register('deleteOldDistFromResources', Delete) {
    delete 'src/main/resources/static'
}

tasks.register('movePongDistToResources', Copy) {
    dependsOn buildPong, deleteOldDistFromResources
    from '../brawl-pong-client/dist'
    into 'src/main/resources/static'
}

processResources.dependsOn movePongDistToResources

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '17'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '17'
}